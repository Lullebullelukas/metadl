buildscript {
    // This configuration is needed to find the JastAdd plugin.
    repositories.mavenCentral()
        dependencies {
        classpath 'org.jastadd:jastaddgradle:1.13.3'
        }
}

apply plugin: 'java'
apply plugin: 'jastadd'
apply plugin: 'idea'
apply plugin: 'eclipse'

defaultTasks 'test'

// Specify where to look for dependencies like Beaver, JFlex:
repositories.mavenCentral()
repositories.mavenLocal()

// Dependency configurations (https://docs.gradle.org/current/userguide/dependency_management.html#sub:configurations):
configurations {
    jflex
    beaver
    jastaddparser
}

// Dependencies are Jar files needed to compile and generate the compiler.
dependencies {
    compile 'net.sf.beaver:beaver-rt:0.9.11'
    compile 'com.opencsv:opencsv:4.3.2'
    compile 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    compile 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    compile 'org.javatuples:javatuples:1.2'

    testCompile('org.junit.jupiter:junit-jupiter-api:5.3.1')
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.3.1')
    testCompile('org.junit.jupiter:junit-jupiter-params:5.3.1')

    jflex 'de.jflex:jflex:1.6.1'
    beaver 'net.sf.beaver:beaver-cc:0.9.11'

    // fictive versions for jastadd and jastaddparser - they
    // are substituted in settings.gradle
    jastaddparser 'org.jastadd:jastaddparser:100.100.100'
    jastadd2 'org.jastadd:jastadd:100.100.100'
}


// This specifies where the source code is located:
sourceSets {
    main.java.srcDirs = [ 'src/java', 'src/gen' ]
    test.java.srcDirs = [ 'src/test' ]
}

// Configuration for the test running.
test {
    maxParallelForks = Runtime.runtime.availableProcessors()

    useJUnitPlatform()
    // {
        // excludeTags 'slow'
        // includeEngines 'junit-jupiter'
    // }
    failFast = false
    dependsOn 'cleanTest'  // This causes tests to always be re-run.

    // Summary of tests
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
    testLogging {
        outputs.upToDateWhen {false}
        // Enable stdout
        showStandardStreams true
        // Log passed/failed tests in the console (see also build/reports/tests):
        events "passed", "skipped", "failed"
    }

    doFirst {
        // Ensure src/gen exists before running JFlex:
        file('tests/output').mkdirs()
        file('tests/evaluation/bottomupout/souffle').mkdirs()
    }

    reports {
        junitXml.enabled = true
    }
    file('./tests/output/souffle').mkdirs()
}

jar {
    // The Main-Class attribute specifies which class should be run when by java -jar compiler.jar.
    manifest.attributes 'Main-Class': 'lang.Compiler'
        destinationDir = projectDir  // Gradle by default puts the Jar in build/libs/.
        archiveName = 'compiler.jar'

        // The following from-specification includes all dependencies in compiler.jar
        // so that it can be run as a separate program easily.
        // This is needed because the Beaver runtime classes are otherwise not included.
        // Source: https://www.mkyong.com/gradle/gradle-create-a-jar-file-with-dependencies/
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

// Before compiling, we should generate some Java code:
compileJava.dependsOn 'generateJava', 'generateScanner', 'generateParser',
    'genMetadlLang',
    'generateConfig', 'generateScannerConfig', 'generateParserConfig'


void makeGenerateJavaTask(String name, String outDir, String srcDir, String pkg, boolean patternGrammar = false) {
    tasks.create(name, org.jastadd.JastAddTask) {
	description 'Generates the compiler AST classes from JastAdd specifications.'
	outputDir = file(outDir)
	sources = fileTree(dir: srcDir, includes: [ '**/*.jrag', '**/*.jadd', '**/*.ast' ])
	options = [ '--package=' + pkg, '--beaver' , '--rewrite=cnta' ]
	if (patternGrammar)
	    options << '--pattern_grammar'
    }
}

makeGenerateJavaTask('generateJava', 'src/gen', 'src/jastadd', 'lang.ast')
makeGenerateJavaTask('generateConfig', 'src/gen', 'src/config/', 'lang.ast.config')

void makeGenerateScannerTask(String name, String outDir, String srcFile) {
    tasks.create(name, JavaExec) {
	description 'Generates the scanner with JFlex.'
	classpath = configurations.jflex
	main = 'jflex.Main'

	// Options to JFlex (http://jflex.de/manual.html#running-jflex):
	args '-d', file(outDir).path, file(srcFile).path
    }
}

makeGenerateScannerTask('generateScanner', 'src/gen/lang/ast', 'src/scanner/scanner.flex')
makeGenerateScannerTask('generateScannerConfig', 'src/gen/lang/ast/config', 'src/config/scanner/scanner.flex')



void makeGenerateParserTask(String name, String outDir, String srcFile) {
    tasks.create(name, JavaExec) {
	description 'Generates the parser with Beaver.'
	classpath = configurations.beaver
	main = 'beaver.comp.run.Make'

	// Options to Beaver (http://beaver.sourceforge.net/):
	args '-d', file(outDir).path,
	    '-t', '-c', '-w',
	    file(srcFile).path
    }
}

makeGenerateParserTask('generateParserConfig', 'src/gen', 'src/config/parser/parser.beaver')


void makeGenerateBeaverTask(String name, String outFile, String srcFile, boolean patternGrammar = false) {
    tasks.create(name, JavaExec) {
	doFirst {
            file('src/gen').mkdirs()
	}

	description 'Generates the parser with Beaver.'
	classpath = configurations.jastaddparser
	main = 'org.jastadd.jastaddparser.Main'

	// Options to Beaver (http://beaver.sourceforge.net/):
	if (patternGrammar)
	    args '--pattern_grammar', file(srcFile).path, file(outFile).path
	else
	    args file(srcFile).path, file(outFile).path
    }
}

void makeGenerateLangTask(String taskName, String outDir, String srcLang) {
    tasks.create('MkDirs' + taskName) {
	doFirst {
	    file(outDir + '/' + srcLang + '/obj/ast').mkdirs()
	    file(outDir + '/' + srcLang + '/pat/ast').mkdirs()
	}
    }

    makeGenerateBeaverTask('generateBeaverPatlang' + taskName, outDir + '/' + srcLang + '/PatLangParser.beaver',
			   'src/lang/' + srcLang + '/pat/parser/lang.parser',
			   true)
    makeGenerateBeaverTask('generateBeaverObjlang' + taskName, outDir + '/' + srcLang + '/ObjLangParser.beaver',
			   'src/lang/' + srcLang + '/obj/parser/lang.parser',
			   false)
    makeGenerateParserTask('generateParserPatlang' + taskName, outDir + '/lang/' + srcLang + '/pat/ast',
			   outDir + '/' + srcLang + '/PatLangParser.beaver')
    makeGenerateParserTask('generateParserObjlang' + taskName, outDir + '/lang/' + srcLang + '/obj/ast',
			   outDir + '/' + srcLang + '/ObjLangParser.beaver')
    makeGenerateScannerTask('generateScannerPatlang' + taskName, outDir + '/lang/' + srcLang + '/pat/ast',
			    'src/lang/' + srcLang + '/pat/scanner/scanner.flex')
    makeGenerateScannerTask('generateScannerObjlang' + taskName, outDir + '/lang/' + srcLang + '/obj/ast',
			    'src/lang/' + srcLang + '/obj/scanner/scanner.flex')
    makeGenerateJavaTask('generateJavaPatlang' + taskName, outDir,
			 'src/lang/' + srcLang + '/pat/jastadd', 'lang.' + srcLang + '.pat.ast', true)
    makeGenerateJavaTask('generateJavaObjlang' + taskName, outDir,
			 'src/lang/' + srcLang + '/obj/jastadd', 'lang.' + srcLang + '.obj.ast', false)

    tasks.create(taskName)

    tasks.getByName(taskName).dependsOn tasks.getByName('generateBeaverPatlang' + taskName)
    tasks.getByName(taskName).dependsOn tasks.getByName('generateParserPatlang' + taskName)

    tasks.getByName(taskName).dependsOn tasks.getByName('generateScannerPatlang' + taskName)
    tasks.getByName(taskName).dependsOn tasks.getByName('generateJavaPatlang' + taskName)

    tasks.getByName(taskName).dependsOn tasks.getByName('generateBeaverObjlang' + taskName)
    tasks.getByName(taskName).dependsOn tasks.getByName('generateParserObjlang' + taskName)
    tasks.getByName(taskName).dependsOn tasks.getByName('generateScannerObjlang' + taskName)
    tasks.getByName(taskName).dependsOn tasks.getByName('generateJavaObjlang' + taskName)

    tasks.getByName('generateParserPatlang' + taskName).dependsOn tasks.getByName('generateBeaverPatlang' + taskName)
    tasks.getByName('generateParserObjlang' + taskName).dependsOn tasks.getByName('generateBeaverObjlang' + taskName)

    tasks.getByName('generateBeaverObjlang' + taskName).dependsOn tasks.getByName('MkDirs' + taskName)
    tasks.getByName('generateBeaverPatlang' + taskName).dependsOn tasks.getByName('MkDirs' + taskName)

    tasks.getByName('generateScannerObjlang' + taskName).dependsOn tasks.getByName('MkDirs' + taskName)
    tasks.getByName('generateScannerPatlang' + taskName).dependsOn tasks.getByName('MkDirs' + taskName)

    tasks.getByName('generateJavaObjlang' + taskName).dependsOn tasks.getByName('MkDirs' + taskName)
    tasks.getByName('generateJavaPatlang' + taskName).dependsOn tasks.getByName('MkDirs' + taskName)
}

makeGenerateLangTask('genMetadlLang', 'src/gen/', 'metadl')

makeGenerateBeaverTask('generateBeaver', 'src/gen/LangParser.beaver', 'src/parser/metadl.parser')
makeGenerateParserTask('generateParser', 'src/gen/lang/ast', 'src/gen/LangParser.beaver')
generateParser.dependsOn 'generateBeaver'


// The following makes the clean task also remove generated code:
clean.dependsOn 'cleanGeneratedJava'

task cleanGeneratedJava(type: Delete) {
    description 'Remove generated Java code.'
    delete file('src/gen')
    delete file('compiler.jar')
    delete file('tests/output')
    delete file('tests/evaluation/bottomupout')
    delete file('./out')
    delete file('./tmp')
}

// Additional flags to the java compilter
compileJava {
    options.compilerArgs << "-Xlint:deprecation"
}
