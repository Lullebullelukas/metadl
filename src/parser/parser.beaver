%class "LangParser";
%package "lang.ast";

%embed {:
	static public class SyntaxError extends RuntimeException { public SyntaxError(String msg) {super(msg);}}
	// Disable syntax error recovery
	protected void recoverFromError(Symbol token, TokenStream in) {
		throw new SyntaxError("Cannot recover from the syntax error");
	}
:};

%terminals VAR_ID, PRED_ID, PRED_REF, LPARA, RPARA, LBRACK, RBRACK,
           NUMERAL, STRING, DOT, COMMA, IMPLIED_BY, EDB, OUTPUT, IMPORT, PRED,
           ATOM, NOT, BIND,  EQ, EQLIST, NEQ, LT, LTE, GT, GTE, ADD, SUB, MUL, DIV,
           TYPEOF, TYPE_TYPE, STRING_TYPE, INTEGER_TYPE, PRED_REF_TYPE, LIST_TYPE;

%typeof  program             =  "Program";
%typeof  clause              =  "Clause";
%typeof  literal             =  "Literal";
%typeof  inclusive_literal   =  "InclusiveLiteral";
%typeof  exclusive_literal   =  "ExclusiveLiteral";
%typeof  bin_excl_term_lit   =  "BinaryExclusiveTermLiteral";
%typeof  bind_literal        =  "BINDLiteral";
%typeof  typeof_literal      =  "TYPEOFLiteral";
%typeof  edb_literal         =  "EDBLiteral";
%typeof  output_literal      =  "OUTPUTLiteral";
%typeof  pred_literal        =  "PREDLiteral";
%typeof  atom_literal        =  "ATOMLiteral";
%typeof  neg_literal         =  "NEGLiteral";
%typeof  eq_literal          =  "EQLiteral";
%typeof  eq_list_literal     =  "EQLISTLiteral";
%typeof  neq_literal         =  "NEQLiteral";
%typeof  lt_literal          =  "LTLiteral";
%typeof  lte_literal         =  "LTELiteral";
%typeof  gt_literal          =  "GTLiteral";
%typeof  gte_literal         =  "GTELiteral";
%typeof  atom                =  "Atom";
%typeof  pred_sym            =  "PredicateSymbol";
%typeof  pred_ref            =  "PredicateRef";
%typeof  variable            =  "Variable";
%typeof  constant            =  "Constant";
%typeof  int_constant        =  "IntConstant";
%typeof  string_constant     =  "StringConstant";
%typeof  term                =  "Term";
%typeof  term_list           =  "List";
%typeof  term_list_no_empty  =  "List";
%typeof  term_list_space     =  "List";
%typeof  term_list_space_no_empty  =  "List";
%typeof  lit_list            =  "List";
%typeof  incl_lit_list       =  "List";
%typeof  clause_list         =  "List";
%typeof  fact                =  "Fact";
%typeof  rule                =  "Rule";
%typeof  expr                =  "Expr";
%typeof  expr_factor         =  "Expr";
%typeof  expr_term           =  "Expr";
%typeof  expr_unit           =  "Expr";
%typeof  add_expr            =  "AddExpr";
%typeof  sub_expr            =  "SubExpr";
%typeof  mul_expr            =  "MulExpr";
%typeof  div_expr            =  "DivExpr";
%typeof  term_list_term      =  "TermList";
%typeof  type_term           =  "Type";
%typeof  concrete_type       =  "ConcreteType";
%typeof  type_type           =  "Type";
%typeof  string_type         =  "StringType";
%typeof  integer_type        =  "IntegerType";
%typeof  pred_ref_type       =  "PredRefType";
%typeof  list_type           =  "ListType";

%goal program;

program =  clause_list.list {: return new Program(list); :};
clause_list = clause.c {: return new List().add(c); :}
            | clause_list.list clause.c {: return list.add(c); :}
            ;

clause = rule
       | fact
       ;

rule = incl_lit_list.heads IMPLIED_BY lit_list.body DOT {: return new Rule(heads, body); :};
fact = incl_lit_list.heads DOT {: return new Fact(heads); :};

literal = inclusive_literal
        | exclusive_literal
        ;

inclusive_literal = edb_literal
                  | output_literal
			      | atom
			      | typeof_literal
				  | import_literal
			      ;

exclusive_literal = neg_literal
                  | bind_literal
                  | bin_excl_term_lit
                  | eq_list_literal
                  | pred_literal
                  | atom_literal
             	  ;

neg_literal = NOT LPARA inclusive_literal.lit RPARA {: return new NEGLiteral(new PredicateSymbol("NOT"), lit); :};

bind_literal = BIND LPARA variable.var COMMA expr.right RPARA {: return new BINDLiteral(new PredicateSymbol("BIND"), var, right); :};
bin_excl_term_lit = eq_literal
                  | neq_literal
                  | lt_literal
                  | lte_literal
                  | gt_literal
                  | gte_literal
                  ;

eq_literal  = EQ  LPARA expr.left COMMA expr.right RPARA {: return new EQLiteral(new PredicateSymbol("EQ"),   left, right); :};
neq_literal = NEQ LPARA expr.left COMMA expr.right RPARA {: return new NEQLiteral(new PredicateSymbol("NEQ"), left, right); :};
lt_literal  = LT  LPARA expr.left COMMA expr.right RPARA {: return new LTLiteral(new PredicateSymbol("LT"),   left, right); :};
lte_literal = LTE LPARA expr.left COMMA expr.right RPARA {: return new LTELiteral(new PredicateSymbol("LTE"), left, right); :};
gt_literal  = GT  LPARA expr.left COMMA expr.right RPARA {: return new GTLiteral(new PredicateSymbol("GT"),   left, right); :};
gte_literal = GTE LPARA expr.left COMMA expr.right RPARA {: return new GTELiteral(new PredicateSymbol("GTE"), left, right); :};

eq_list_literal = EQLIST LPARA term_list_term.terms RPARA {: return new EQLISTLiteral(new PredicateSymbol("EQLIST"),   terms); :};

atom = pred_sym.pred LPARA term_list.terms RPARA                      {: return new Atom(pred, terms); :};
edb_literal    = EDB    LPARA pred_ref.pred COMMA term.filelocs RPARA {: return new EDBLiteral(new PredicateSymbol("EDB"), pred, filelocs);  :};
output_literal = OUTPUT LPARA term.t RPARA                            {: return new OUTPUTLiteral(new PredicateSymbol("OUTPUT"), t);         :};
pred_literal   = PRED   LPARA term.t RPARA                            {: return new PREDLiteral(new PredicateSymbol("PRED"), t);             :};
atom_literal   = ATOM   LPARA term.t RPARA                            {: return new ATOMLiteral(new PredicateSymbol("ATOM"), t);             :};
typeof_literal = TYPEOF LPARA term.pred COMMA term.t RPARA            {: return new TYPEOFLiteral(new PredicateSymbol("TYPEOF"), pred, t);   :};
import_literal = IMPORT LPARA term.file_loc
                        COMMA pred_ref.program_ref
                        COMMA pred_ref.head_ref
                        COMMA pred_ref.body_ref
                        COMMA pred_ref.fact_ref
                        COMMA pred_ref.atom_ref
                        COMMA pred_ref.predicate_symbol_ref
                        COMMA pred_ref.int_constant_ref
                        COMMA pred_ref.string_constant_ref
                        COMMA pred_ref.predicate_ref_ref
                        COMMA pred_ref.variable_ref
                        COMMA pred_ref.import_literal_ref
                        COMMA pred_ref.edb_literal_ref
                        COMMA pred_ref.output_literal_ref
                        RPARA
     {: return new IMPORTLiteral(new PredicateSymbol("IMPORT"), file_loc, program_ref, head_ref, body_ref, fact_ref, atom_ref, predicate_symbol_ref,
                       int_constant_ref, string_constant_ref, predicate_ref_ref, variable_ref,
                       import_literal_ref, edb_literal_ref, output_literal_ref); :};


lit_list = literal.lit {: return new List().add(lit); :}
         | lit_list.list COMMA literal.c {: return list.add(c); :}
         ;

incl_lit_list = inclusive_literal.lit {: return new List().add(lit); :}
         | incl_lit_list.list COMMA inclusive_literal.lit {: return list.add(lit); :}
         ;

term_list = term_list_no_empty
          | /* epsilon */ {: return new List(); :}
          ;

term_list_no_empty = term.t {: return new List().add(t); :}
                   | term_list_no_empty.list COMMA term.t {: return list.add(t); :}
                   ;

expr = expr_term;

expr_term   = add_expr
            | sub_expr
            | expr_factor
            ;

expr_factor = mul_expr
            | div_expr
            | expr_unit
            ;

expr_unit = term
	      | LPARA expr.e RPARA {: return e; :}
	      ;

add_expr = expr_term.e1   ADD expr_factor.e2 {: return new AddExpr(e1, e2); :};
sub_expr = expr_term.e1   SUB expr_factor.e2 {: return new SubExpr(e1, e2); :};
mul_expr = expr_factor.e1 MUL expr_unit.e2   {: return new MulExpr(e1, e2); :};
div_expr = expr_factor.e1 DIV expr_unit.e2   {: return new DivExpr(e1, e2); :};

term = variable
     | constant
     | pred_ref
     | term_list_term
     | type_term
     ;

pred_sym = PRED_ID.id  {: return new PredicateSymbol(id); :};
pred_ref = PRED_REF.id {: return new PredicateRef(id); :};

variable = VAR_ID.id {: return new Variable(id); :};
constant = int_constant
         | string_constant
         ;

int_constant    = NUMERAL.num {: return new IntConstant(num); :};
string_constant = STRING.str {: return new StringConstant(str); :};
term_list_term  = LBRACK term_list_space.tl RBRACK {: return new TermList(tl); :};

term_list_space = term_list_space_no_empty
          | /* epsilon */ {: return new List(); :}
          ;

term_list_space_no_empty = term.t {: return new List().add(t); :}
                   | term_list_space_no_empty.list term.t {: return list.add(t); :}
                   ;

type_term = concrete_type
          | list_type
          ;

type_type = TYPE_TYPE {: return new TypeType(); :};

concrete_type = integer_type
              | string_type
              | pred_ref_type
              | type_type
              ;

integer_type  = INTEGER_TYPE  {: return new IntegerType(); :};
string_type   = STRING_TYPE   {: return new StringType();  :};
pred_ref_type = PRED_REF_TYPE {: return new PredRefType(); :};
list_type     = LIST_TYPE LPARA concrete_type.embell RPARA {: return new ListType(embell); :};
