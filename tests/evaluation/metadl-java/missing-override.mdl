IMPORT('P, "tests/evaluation/metadl-java/src/missing-override/", "java5").

SuperClass(0, 0), SuperInterface(0, 0) :- NEQ(0, 0).
MethodSig1(0, "", 0), MethodSig2(0, 0, 0) :- NEQ(0, 0).

# Code for extracting the type hierarchy
analyze('P, "java5") {
	    SuperClass1(c, d) :- c <: class `_ extends `d { .. } :>, DECL(`d, d).

	    # Lookup the declaration for Object
	    ObjectDecl(o) :- u <: Object :>, DECL(u, o).

	    # If the class does not extend any other class, then it extends Object
	    SuperClass(c, o) :- c <: class `_ { .. } :>, NOT(SuperClass1(c, _)), ObjectDecl(o).
	    SuperClass(c, d) :- SuperClass1(c, d).

	    SuperInterface(c, d) :- c <: class `_ implements .., `i, .. { .. } :>, DECL(`i, d).
	    SuperInterface(c, d) :- c <: enum `_ implements .., `i, .. { .. } :>, DECL(`i, d).
	    SuperInterface(c, d) :- c <: interface `_ extends .. ,`i, .. { .. } :>, DECL(`i, d).
	    SuperType(t, s) :- SuperClass(t, s).
	    SuperType(t, s) :- SuperInterface(t, s).


	    TSuperType(t, s) :- SuperType(t, s).
	    TSuperType(t, s) :- TSuperType(t, s1), SuperType(s1, s).
}

# Code for finding if a method overrides another method
analyze('P, "java5") {
	    MethodSig2(m, i, pt) :- m <: `rett `name(.., `pt `_, ..) ; :>,
	    		  DECL(`pt, pt), PARENT(param, `pt), INDEX(param, i).
	    # TODO: The MethodSig1 relation will not contain Object.equals(Object o), since the DECL attribute
	    # is not exported for Object because it is an NTA.
	    MethodSig1(m, name, ret) :- m <: `rett `name(..) ; :>,
	    		  DECL(`rett, ret), ID(`name, name).

	    MethodHasNoParams(m) :- MethodSig1(m, _, _), NOT(MethodSig2(m, _, _)).
	    DiffNumParams(m1, m2), DiffNumParams(m2, m1) :- MethodSig2(m1, i, _), MethodSig2(m2, _, _), NOT(MethodSig2(m2, i, _)), NEQ(m1, m2).
	    DiffNumParams(m1, m2), DiffNumParams(m2, m1) :- MethodSig2(m1, _, _), MethodHasNoParams(m2).

	    DiffParamTypes(m1, m2), DiffParamTypes(m2, m1) :- MethodSig2(m1, i, t1), MethodSig2(m2, i, t2), NEQ(t1, t2).

	    SameSig(m1, m2) :- MethodSig1(m1, n, ret), MethodSig1(m2, n, ret), NOT(DiffNumParams(m1, m2)), NOT(DiffParamTypes(m1, m2)).


	    GrandParent(g, c) :- PARENT(g, p), PARENT(p, c).

	    # Covariant return type
	    Override(m, super) :- MethodSig1(m, n, ret), MethodSig1(super, n, ret_super), TSuperType(ret, ret_super),
                                  GrandParent(c1, m), GrandParent(c2, super), TSuperType(c1, c2), NOT(DiffNumParams(m, super)).
	    # Equal return type
	    Override(m, super) :- MethodSig1(m, n, ret), MethodSig1(super, n, ret),
                                  GrandParent(c1, m), GrandParent(c2, super), TSuperType(c1, c2), NOT(DiffNumParams(m, super)).

}

# Analysis
analyze('P, "java5") {
	    HasOverride(m) :- m <: .. @Override .. `t `n(..) ; :>.
	    MissingOverride(m) :- Override(m, _), NOT(HasOverride(m)).
	    MissingOverrideDetail(l, c, file) :- MissingOverride(m), SRC(m, l, c, _, _, file).
}

OUTPUT('MissingOverrideDetail).
